apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.github.havardh'
version = '1.5.1-SNAPSHOT'
mainClassName = "com.github.havardh.javaflow.JavaFlow"

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
     maven { url "http://repo.maven.apache.org/maven2" }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

junitPlatform {
    platformVersion '1.0.2'
    // enableStandardTestTask true
    // reportsDir "build/test-results/junit-platform" // this is the default
    logManager 'org.apache.logging.log4j.jul.LogManager'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task writeNewPom {
    doLast {
        pom {
            project {
                inceptionYear '2017'
            }
        }.writeTo("$buildDir/pom.xml")
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'javaflow'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Java model to flowtype converter'
                url 'https://github.com/havardh/javaflow'

                scm {
                    connection 'scm:git:git://github.com/havardh/javaflow'
                    developerConnection 'scm:git:ssh://github.com:havardh/javaflow.git'
                    url 'https://github.com/havardh/javaflow/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'havardh'
                        name 'Håvard Wormdal Høiby'
                        email 'havardwhoiby@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile group: 'com.github.javaparser', name: 'javaparser-core', version:'3.7.0'
    compile group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version:'1.08'
    testRuntime group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.7'
    testRuntime group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version:'5.0.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version:'5.0.2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    testCompile group: 'com.google.code.findbugs', name: 'annotations', version:'2.0.1'
}